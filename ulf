#!/usr/bin/perl -w

# The MIT License (MIT)
#
# Copyright (c) 2014 Johannes Bauer
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
# THE SOFTWARE.

use strict;
use warnings;


usage() if $#ARGV < 1;

# Stats
my %stats;

# existing phrases in language file ($langFile)
my %lang;

# Command line
my ($langFile, $baseFile, $outFile) = @ARGV;


#
# Language file (in)
#

open IN_LANG, "< :encoding(UTF-16)", $langFile
	or die "$langFile won't open: $!\n";

while (<IN_LANG>) {

	# looking for comments
	if (m/^\/\*/) {

		my $comment = $_;

		# if a line is a comment => the next line is a translation
		my $translation = <IN_LANG>;

		if ($translation =~ m/^(\".+\") =/) {

			# save comment + translation using the key of the translation
			$lang{$1} = "$comment$translation\n";

			$stats{"langPhrases"}++;
		}
	}
}

close IN_LANG;

#
# Base file (in)
#

my $result;

open IN_BASE, "< :encoding(UTF-16)", $baseFile
	or die "$baseFile won't open $!\n";

while (<IN_BASE>) {

	if (m/^\/\*/) {

		my $comment = $_;
		my $translation = <IN_BASE>;

		if ($translation =~ m/^(\".+\") =/) {

			# if the translation already exists in the language file then keep it
			# unless use comment + translation of the base file
			$result .= $lang{$1} ? $lang{$1} : "$comment$translation\n";

			$stats{"basePhrases"}++;
		}
	}
}

close IN_BASE;

#
# Language file (out)
#

if ($outFile) {

	open OUT_FILE, "> :encoding(UTF-16)", $outFile
	or die "$outFile won't open: $!\n";

	print OUT_FILE $result;

	close OUT_FILE;
}

else {
	print $result;
}

#
# Stats
#

print STDERR "Result: $stats{'basePhrases'} base phrases, $stats{'langPhrases'} language phrases\n\n";

exit;

#
# USAGE
#

sub usage {

	my @split = split("/", $0);

	print "usage: $split[-1] <lang_file> <base_file> [<out_file>]\n\n";
	exit;
}



